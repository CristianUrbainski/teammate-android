/*
 * MIT License
 *
 * Copyright (c) 2019 Adetunji Dahunsi
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.0'
    defaultConfig {
        applicationId "com.mainstreetcode.teammate"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 41
        versionName "4.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true  //add this line

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    signingConfigs {
        release {
            def keystoreProps = new Properties()
            def propertiesFile = file("keystore.properties")
            keystoreProps.load(new FileInputStream(propertiesFile))
            storeFile file("release.keystore")
            keyAlias keystoreProps['alias']
            keyPassword keystoreProps['aliasPassword']
            storePassword keystoreProps['keystorePassword']
        }
        staging {
            def keystoreProps = new Properties()
            def propertiesFile = file("keystore.properties")
            keystoreProps.load(new FileInputStream(propertiesFile))
            storeFile file("release.keystore")
            keyAlias keystoreProps['alias']
            keyPassword keystoreProps['aliasPassword']
            storePassword keystoreProps['keystorePassword']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            buildConfigField "boolean", "DEV", "true"
        }
        staging {
            debuggable false
            signingConfig signingConfigs.staging
            buildConfigField "boolean", "DEV", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "DEV", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // Keep the following configuration in order to target Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    viewBinding {
        enabled = true
    }
}

dependencies {
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
    kapt "androidx.room:room-compiler:2.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.emoji:emoji:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.fragment:fragment:1.2.0-alpha04'
    implementation 'androidx.fragment:fragment-ktx:1.2.0-alpha04'

    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.room:room-runtime:2.1.0'
    implementation 'androidx.room:room-rxjava2:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'

    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'com.google.android.material:material:1.1.0-alpha10'
    implementation 'com.google.android.libraries.places:places:2.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.3'

    implementation 'com.tunjid.androidx:recyclerview:1.0.0-beta01'
    implementation 'com.tunjid.androidx:navigation:1.0.0-beta01'
    implementation 'com.tunjid.androidx:savedstate:1.0.0-beta01'
    implementation 'com.tunjid.androidx:functions:1.0.0-beta01'
    implementation 'com.tunjid.androidx:material:1.0.0-beta01'
    implementation 'com.tunjid.androidx:view:1.0.0-beta01'
    implementation 'com.tunjid.androidx:core:1.0.0-beta01'

    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation('com.facebook.android:facebook-login:4.41.0') {
        exclude module: 'support-media-compat'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'exifinterface'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v13'
        exclude module: 'support-v4'
        exclude module: 'customtabs'
        exclude module: 'design'
        exclude group: 'com.google.zxing', module: 'core'
    }
    implementation('com.devbrackets.android:exomedia:4.3.0') {
        exclude module: 'support-media-compat'
        exclude module: 'appcompat-v7'
    }
    implementation('io.socket:socket.io-client:1.0.0') {
        exclude group: 'org.json', module: 'json'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.room:room-testing:2.1.0'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    exclude group: 'com.android.support', module: 'support-media-compat'
}
apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
